using Unitful: Temperature, Density, Velocity, @derived_dimension
using Unitful: ğ‹, ğ“, ğŒ, ğš¯, ğ

@derived_dimension HeatCapacity ğ‹^2 * ğ“^-2 * ğš¯^-1
@derived_dimension MolarMass ğŒ * ğ^-1
@derived_dimension MomentumDensity ğŒ * ğ‹^-2 * ğ“^-1
@derived_dimension SpecificEnergy ğ‹^2 * ğ“^-2
@derived_dimension EnergyDensity ğŒ * ğ‹^-1 * ğ“^-2

# default units for certain quantities.
const _units_cvcp = u"J/kg/K"
const _units_â„³ = u"kg/mol"
const _units_Ï = u"kg/m^3"
const _units_v = u"m/s"
const _units_T = u"K"

const _units_Ïv = _units_Ï * _units_v
const _units_int_e = _units_cvcp * _units_T
const _units_ÏE = _units_Ï * _units_int_e

"""
    CaloricallyPerfectGas
Provides the properties of a calorically perfect gas (or mixture of gases).
 - ``c_p``: Heat capacity at constant pressure
 - ``c_v``: Heat capacity at constant volume
 - ``â„³``: Molar mass
 - ``Î³``: Heat capacity ratio
 - ``R``: Gas constant
"""
struct CaloricallyPerfectGas{U1<:HeatCapacity,U2<:MolarMass}
    c_p::U1
    c_v::U1
    â„³::U2

    Î³::Float64
    R::U1
end

function CaloricallyPerfectGas(c_p, c_v, â„³)
    q_cp = Quantity(c_p, _units_cvcp)
    q_cv = Quantity(c_v, _units_cvcp)
    q_â„³ = Quantity(â„³, _units_â„³)
    q_R = q_cp - q_cv
    return CaloricallyPerfectGas{typeof(q_cp),typeof(q_â„³)}(q_cp, q_cv, q_â„³, c_p / c_v, q_R)
end

function CaloricallyPerfectGas(c_p::T, c_v::T, â„³::U) where {T<:HeatCapacity,U<:Unitful.MolarMass}
    R = c_p - c_v
    Î³ = c_p / c_v
    return CaloricallyPerfectGas{T,U}(c_p, c_v, â„³, Î³, R)
end

"""
    enthalpy(T; gas::CaloricallyPerfectGas)
Computes the enthalpy of a calorically perfect gas at a temperature ``T``.
"""
enthalpy(T; gas::CaloricallyPerfectGas) = gas.c_p * Quantity(T, _units_T)
enthalpy(T::Temperature; gas::CaloricallyPerfectGas) = gas.c_p * T

"""
    internal_energy(T; gas::CaloricallyPerfectGas)
Computes the internal energy of a calorically perfect gas.
"""
internal_energy(T; gas::CaloricallyPerfectGas) = gas.c_v * Quantity(T, _units_T)
internal_energy(T::Temperature; gas::CaloricallyPerfectGas) = gas.c_v * T

"""
    speed_of_sound(T; gas::CaloricallyPerfectGas)
Computes the speed of sound in an ideal gas at a temperature ``T``. 

*We assume that the gas is a non-dispersive medium.*
"""
speed_of_sound(T; gas::CaloricallyPerfectGas) = sqrt(gas.Î³ * gas.R * Quantity(T, _units_T))
speed_of_sound(T::Temperature; gas::CaloricallyPerfectGas) = sqrt(gas.Î³ * gas.R * T)


"""
    pressure(Ï, T; gas::CaloricallyPerfectGas)
Compute the pressure in a calorically perfect gas from its density and temperature.
"""
function pressure(Ï, T; gas::CaloricallyPerfectGas)
    return (gas.Î³ - 1) * Quantity(Ï, _units_Ï) * internal_energy(T; gas=gas)
end

function pressure(Ï::Density, T::Temperature; gas::CaloricallyPerfectGas)
    return (gas.Î³ - 1) * Ï * internal_energy(T; gas=gas)
end

"""
    pressure(Ïe; gas::CaloricallyPerfectGas)
Compute the pressure in a calorically perfect gas from its internal energy density.
"""
pressure(Ïe; gas::CaloricallyPerfectGas) = (gas.Î³ - 1) * Quantity(Ïe, _units_ÏE)
pressure(Ïe::EnergyDensity; gas::CaloricallyPerfectGas) = (gas.Î³ - 1) * Ïe

"""
Properties, that are easier to reason about than those in a `ConservedState`. 
These completely determine the state of a calorically perfect gas.

 - ``Ï``: Density of the gas.
 - ``M``: The mach number, represented as a vector quantity.
 - ``T``: The absolute temperature of the gas.
"""
struct PrimitiveState{U1<:Density,U2<:Temperature}
    Ï::U1
    M::Vector{Float64}
    T::U2
end

"""
    PrimitiveState(Ï::Float64, M::Vector{Float64}, T::Float64)
Construct a PrimitiveState and assign the default units.
"""
function PrimitiveState(Ï::U, M::Vector{U}, T::U) where {U}
    return PrimitiveState(Quantity(Ï, _units_Ï), M, Quantity(T, _units_T))
end

"""
    ConservedState{<:Density, <:MomentumDensity, <:EnergyDensity}
The conserved quantities in the Euler equations.

 - ``Ï``: Density of the gas.
 - ``Ïv``: Momentum density of the gas, represented as a vector quantity.
 - ``ÏE``: The **sum** of the internal energy density of the gas and kinetic energy density of the moving gas.
"""
struct ConservedState{U1<:Density,U2<:MomentumDensity,U3<:EnergyDensity}
    Ï::U1
    Ïv::Vector{U2}
    ÏE::U3
end

"""
    PrimitiveState(state::ConservedState; gas::CaloricallyPerfectGas)
Compute the primitive state quantities from the conserved state quantities and 
the thermodynamic properies of the gas.
"""
function PrimitiveState(state::ConservedState; gas::CaloricallyPerfectGas)
    a = speed_of_sound(state; gas=gas)
    # mach number should be dimensionless and unitless (we avoid e.g. 2.0u"mm/m")
    return PrimitiveState(state.Ï, uconvert.(NoUnits, state.Ïv / (state.Ï * a)), temperature(state, gas=gas))
end

"""
    ConservedState(Ï::Float64, Ïv::Vector{Float64}, ÏE::Float64)
Construct a ConservedState and assign the default units.
"""
function ConservedState(Ï::T, Ïv::Vector{T}, ÏE::T) where {T}
    return ConservedState(Quantity(Ï, _units_Ï), Quantity.(Ïv, _units_Ïv), Quantity(ÏE, _units_ÏE))
end

"""
    ConservedState(state::PrimitiveState; gas::CaloricallyPerfectGas)
Compute the conserved state quantities from primitive state quantities and
the thermodynamic properties of a gas.
"""
function ConservedState(state::PrimitiveState; gas::CaloricallyPerfectGas)
    v = state.M * speed_of_sound(state; gas=gas)
    e = gas.c_v * state.T
    return ConservedState(state.Ï, state.Ï * v, state.Ï * (e + v â‹… v / 2))
end

"""
    internal_energy_density(Ï, Ïv, ÏE)
    internal_energy_density(state::ConservedState)
Compute the internal energy volume density (``Ïe``) from conserved state quantities.
"""
internal_energy_density(Ï, Ïv, ÏE) = ÏE - (Ïv â‹… Ïv) / (2 * Ï)
internal_energy_density(state::ConservedState) = internal_energy_density(state.Ï, state.Ïv, state.ÏE)

"""
    internal_energy(state; gas::CaloricallyPerfectGas)
Compute the internal energy (``e``) of a gas at a given state.
"""
internal_energy(state::ConservedState; gas::CaloricallyPerfectGas) = internal_energy_density(state) / state.Ï
internal_energy(state::PrimitiveState; gas::CaloricallyPerfectGas) = gas.c_v * state.T

"""
    pressure(state; gas::CaloricallyPerfectGas)
Compute the pressure at a given state in a gas.
"""
pressure(state::ConservedState; gas::CaloricallyPerfectGas) = pressure(internal_energy_density(state); gas=gas)
pressure(state::PrimitiveState; gas::CaloricallyPerfectGas) = pressure(state.Ï, state.T; gas=gas)

"""
    density(state)
Compute the density at a given state in a gas.
"""
density(state::Union{ConservedState,PrimitiveState}) = state.Ï

"""
    temperature(state; gas::CaloricallyPerfectGas)
Compute the temperature at a given state in a gas.
"""
function temperature(state::ConservedState; gas::CaloricallyPerfectGas)
    return internal_energy(state; gas=gas) / gas.c_v
end

temperature(state::PrimitiveState; gas::CaloricallyPerfectGas) = state.T

"""
    speed_of_sound(state::Union{ConservedState, PrimitiveState}; gas::CaloricallyPerfectGas)
Compute the speed of sound in a gas at a given state. 

*We assume that the gas is a non-dispersive medium.*
"""
function speed_of_sound(state::Union{ConservedState,PrimitiveState}; gas::CaloricallyPerfectGas)
    return speed_of_sound(temperature(state; gas=gas); gas=gas)
end

### DISRESPECT UNITS AND WORK WITH STATES AS COLLECTIONS ###

function state_to_vector(state::T) where {T<:Union{PrimitiveState,ConservedState}}
    return vcat(map(sym -> ustrip.(getfield(state, sym)), fieldnames(T))...)
end

"""
    primitive_state_vector(u; gas::CaloricallyPerfectGas)

Takes a vector of conserved quantities ``u=[Ï, Ïv, ÏE]`` and converts it into
``s=[Ï, M, T]``. 

**Assumes that everything is given in metric base units!**
"""
function primitive_state_vector(u; gas::CaloricallyPerfectGas)
    Ïv = u[2:end-1]
    Ïe = internal_energy_density(u[1], Ïv, u[end])
    T = Ïe / (u[1] * ustrip(_units_cvcp, gas.c_v))
    a = ustrip(u"m/s", speed_of_sound(T, gas=gas))
    return vcat(u[1], Ïv / (u[1] * a), T)
end

function conserved_state_vector(s; gas::CaloricallyPerfectGas)
    a = ustrip(u"m/s", speed_of_sound(s[end]; gas=gas))
    Ïv = s[1] * s[2:end-1] * a
    Ïe = s[1] * ustrip(_units_cvcp, gas.c_v) * s[end]
    ÏE = Ïe + Ïv â‹… Ïv / (2 * s[1])
    return vcat(s[1], Ïv, ÏE)
end